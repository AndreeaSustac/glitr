description = "GLiTR"

group = 'com.nfl.glitr'
version = '1.1.0-SNAPSHOT'

buildscript {
    repositories {
        maven {
            url "http://nexus.la3.nfl.com/nexus/content/groups/public"
        }
        mavenCentral()
    }
    dependencies {
        classpath 'com.nfl.dm.shield.gradle:shield-plugin:1.0.+'
    }
}

apply plugin: 'com.nfl.dm.shield.gradle'
apply plugin: 'idea'
apply plugin: 'groovy'

jacoco {
    toolVersion = "0.7.5.201505241946"
}

//noinspection GroovyUnusedAssignment
sourceCompatibility = 1.8
//noinspection GroovyUnusedAssignment
targetCompatibility = 1.8

repositories {
    mavenLocal()
    maven {
        url "http://nexus.la3.nfl.com/nexus/content/groups/public"
    }
    mavenCentral()
    maven { url  "http://dl.bintray.com/andimarek/graphql-java" }
}

dependencies {
    // GraphQL dependencies
    compile("com.graphql-java:graphql-java:2.1.0")

    // Commons dependencies
    compile("org.apache.commons:commons-lang3:3.4")
    compile("commons-beanutils:commons-beanutils:1.9.2")

    // Google dependencies
    compile("com.google.code.findbugs:jsr305:3.0.0") // TODO: is this needed?
    compile("com.google.guava:guava:19.0")
    compile("com.googlecode.gentyref:gentyref:1.2.0")

    // Jackson dependencies
    compile("com.fasterxml.jackson.core:jackson-core:2.6.2")
    compile("com.fasterxml.jackson.core:jackson-databind:2.6.2")
    compile("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.6.4") // handles jackson new java8 Date APIs

    // Misc dependencies
    compile("io.reactivex:rxjava:1.1.3")
    compile("javax.validation:validation-api:1.1.0.Final")

     // Spock test dependencies
    testCompile("junit:junit:4.12")
    testCompile("org.spockframework:spock-core:1.0-groovy-2.4")
    testCompile("org.codehaus.groovy:groovy-all:2.4.4")
}

task testSpock(type: Test) {
    useJUnit()
    systemProperty 'consul_hostname', 'la3devconsul01.dm.nfl.com'
    systemProperty 'consul_folder', project.ext.appName
}

test.dependsOn 'testSpock'

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}

sonarqube {
    //noinspection GroovyAssignabilityCheck
    properties {
        //noinspection GroovyAssignabilityCheck
        property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/testSpock.exec"
        //noinspection GroovyAssignabilityCheck
        property "sonar.coverage.exclusions", "**/*Exception.java"
    }
}

//noinspection GroovyAssignabilityCheck
configurations.all {
    // check for updates every build
    // noinspection GroovyAssignabilityCheck
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

sourceSets {
    //tell gradle where the resource folders (used for inclusion in the jar too)
    main {
        resources.srcDirs = ["src/main/resources", "generatedsources/"]
    }
}

jar {
    from "LICENSE"
    manifest {
        attributes(
                "Specification-Title": project.name,
                "Specification-Version": project.version,
                "Implementation-Version": project.version,
        )
    }
}
